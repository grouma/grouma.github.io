---
layout: post
title: Homemade Sous-Vide Cooker
comments: true
---

I stumbled across the <a href="http://en.wikipedia.org/wiki/Sous-vide">sous-vide</a> method while browsing <a href="http://www.reddit.com/r/cooking">/r/cooking</a>. I was immediately intrigued by the novel concept but did not want to shell out a large sum of money for an unproven device. Thankfully, I had a spare Arduino and was looking for another side project to hack on.

The idea of making a sous-vide cooker with an Arduino is not a new. In fact there are a number of fantastic tutorials that can easily be found with a quick search. With that in mind, I will simply discuss some of my unique decisions in the process of making the device.

### Relay

I chose to go with a pre-made relay because I'll be the first to admit that I do not know much about electronics. I decided it is best to try and limit the chance of shock while I'm still learning. Unfortunately, the most popular AC power switch, PowerSwitch Tail II, was on backorder from all major retailers and took several weeks to ship. The demand is understandable though as it makes controlling household items dead simple.

### Input

I did not want to upload new code each time I wanted to make use of the device. Thus hardcoding the desired temperature was out of the question. Instead I added a simple potentiometer which was used to select a cooking temperature from 0 - 99 degrees Celsius.

### Temperature Gauge

Initially I wanted to make a completely custom made temperature gauge using a thermistor and <a href="http://en.wikipedia.org/wiki/Steinhart%E2%80%93Hart_equation">Steinhart-Hart's equation</a>. Getting accurate readings with this method was very easy but making the gauge waterproof proved futile with my terrible handiwork. Out of frustration I opted to go with a pre-made digital temperature sensor. Setting up the gauge was very simple.

### Slow Cooker

I wanted a barebones slow-cooker for both price and ease of use. I found a 3 quart slow cooker for only $20 on Amazon. The benefit of such a small cooker is that you don't have to worry about fluctuations of temperature within the sous-vide water bath. Most professional sous-vide cookers prevent this issue by integrating a small water jet.

### Completed Device

![completed sous-vide cooker 1](/public/images/sous-vide-1.jpg)
![completed sous-vide cooker 2](/public/images/sous-vide-2.jpg)


### Materials

* Arduino Uno
* Various hookup wires
* LCD Display
* Potentiometer x 2
* 9v Battery
* Breadboard x 2
* Project Box
* <a href="http://www.amazon.com/Crock-Pot-SCR300SS-3-Quart-Manual-Stainless/dp/B003UCG8II/ref=sr_1_3?ie=UTF8&qid=1404616855&sr=8-3&keywords=crockpot">Simple Crock-Pot</a>
* <a href="https://www.adafruit.com/products/381">DS18B20 Digital Temperature Sensor</a>
* <a href="https://www.sparkfun.com/products/10747">PowerSwitch Tail II</a>
* <a href="http://www.amazon.com/Ziploc-Vacuum-Starter-3-Quart-1-Pump/dp/B003UEMFUG/ref=sr_1_1?ie=UTF8&qid=1404617144&sr=8-1&keywords=Ziploc+Vacuum+Starter+Kit">Ziploc Vacuum Starter Kit</a>

### Code

The code is very straightforward. I'm leveraging this <a href="https://github.com/JChristensen/Timer">Timer</a> library along with this <a href="https://www.pjrc.com/teensy/td_libs_OneWire.html">OneWire</a> library for controlling the digital temperature gauge. One interesting fact is that I read the temperature every 10 seconds. This is because the act of polling for the temperature is quite computationally expensive. Unfortunately, I'm not sure exactly why as I did not dig into the library. Regardless, I did not like the noticeable input lag when I was using the potentiometer to set the desired cooking temperature.

{% highlight C %}

#include <Timer.h>
#include <LiquidCrystal.h>
#include <OneWire.h>
#include <DallasTemperature.h>

// Define the input pin for the potentiometer that will set desired temperature
#define INPUT_PIN 5
// Define One Wire data pin
#define ONE_WIRE_BUS 13
// Define Pin used to control Powerswitch
#define POWERSWITCH_PIN A0


// Setup a One Wire instance to communicate with DS18B20
OneWire oneWire(ONE_WIRE_BUS);
DallasTemperature sensors(&oneWire);

// Initialize the library with the numbers of the interface pins
LiquidCrystal lcd(12, 11, 5, 4, 3, 2);

Timer timer;
int currentTemp = 0;
int previousTemp = -1;
int previousDesiredTemp = -1;

void setup() {
  
  // Set up the LCD's number of columns and rows: 
  lcd.begin(16, 2);
  lcd.setCursor(0,0);
  lcd.print("Current temp: ");
  lcd.setCursor(0,1);
  lcd.print("Desired temp: ");
  
  // Set powerswitch pin to output
  pinMode(POWERSWITCH_PIN, OUTPUT);

  // Set up DS18B20
  sensors.begin();
 
  getCurrentTemperature();
  // Read termperature every 10 seconds
  // This reduces input lag when setting desired temperature
  timer.every(10000, getCurrentTemperature);
}

void getCurrentTemperature()
{
  sensors.requestTemperatures();
  // Only one sensor is attached
  currentTemp = sensors.getTempCByIndex(0);
}

void loop() {
  
  timer.update();
  
  // Only update screen when necessary to avoid flashing
  if(currentTemp != previousTemp){
    previousTemp = currentTemp;
    lcd.setCursor(14, 0);
    lcd.print("  "); // Clear current number
    lcd.setCursor(14, 0);
    lcd.print(String(currentTemp));
  }
  
  // Read desired temperature and convert to value between 0 - 99
  int desiredTemp = floor((analogRead(INPUT_PIN)/1024.0) * 100);
  
  // Again only update screen when necessary to avoid flashing
  if(desiredTemp != previousDesiredTemp){
    previousDesiredTemp = desiredTemp;
    lcd.setCursor(14, 1);
    lcd.print("  "); // Clear current number
    lcd.setCursor(14, 1);
    lcd.print(String(desiredTemp));
  }
  
  if(desiredTemp > currentTemp){
    // Turn crock pot on
    digitalWrite(POWERSWITCH_PIN, LOW);
  }else{
    // Turn crock pot off
    digitalWrite(POWERSWITCH_PIN, HIGH);
  }
  
}

{% endhighlight %}